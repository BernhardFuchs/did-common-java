did                = "did:" method ":" method-specific-id ;
method             = 1*method-char ;
method-char        = %x61-7A / DIGIT ;
method-specific-id = idstring *( ":" idstring ) ;
idstring           = 1*idchar ;
idchar             = ALPHA / DIGIT / "." / "-" ;
did-url            = did *( ";" param ) path-abempty
                     [ "?" query ] [ "#" fragment ] ;
param              = param-name [ "=" param-value ] ;
param-name         = 1*param-char ;
param-value        = *param-char ;
param-char         = ALPHA / DIGIT / "." / "-" / "_" / pct-encoded ;


      path-abempty  = *( "/" segment ) ;
      path-absolute = "/" [ segment-nz *( "/" segment ) ] ;
      path-noscheme = segment-nz-nc *( "/" segment ) ;
      path-rootless = segment-nz *( "/" segment ) ;

   segment       = *pchar ;
   segment-nz    = 1*pchar ;
   segment-nz-nc = 1*( unreserved / pct-encoded / sub-delims / "@" ) ;

   pchar         = unreserved / pct-encoded / sub-delims / ":" / "@" ;

   query         = *( pchar / "/" / "?" ) ;

   fragment      = *( pchar / "/" / "?" ) ;

   pct-encoded   = "%" HEXDIG HEXDIG ;

   unreserved    = ALPHA / DIGIT / "-" / "." / "_" / "~" ;
   reserved      = gen-delims / sub-delims ;
   gen-delims    = ":" / "/" / "?" / "#" / "[" / "]" / "@" ;
   sub-delims    = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "=" ;


         ALPHA          =  %x41-5A / %x61-7A ;
         DIGIT          =  %x30-39 ;
         HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F" ;
